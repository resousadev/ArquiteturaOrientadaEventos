plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'checkstyle'
}

group = 'io.resousadev.linuxtips'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// PostgreSQL Driver
	runtimeOnly 'org.postgresql:postgresql'

	// Flyway for database migrations
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'

	// AWS SDK for EventBridge
	implementation 'software.amazon.awssdk:eventbridge:2.38.5'

	// MapStruct for object mapping
	implementation 'org.mapstruct:mapstruct:1.6.3'

	// Spring Dotenv for environment variable management
	implementation 'me.paulschwarz:spring-dotenv:4.0.0'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	
	// Logback with JSON structured logging support
	implementation 'net.logstash.logback:logstash-logback-encoder:8.0'

	// Lombok - must be before MapStruct annotation processor
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

configurations {
    all {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}

checkstyle {
	toolVersion = '10.12.5'
	configFile = file("${projectDir}/config/checkstyle/checkstyle.xml")
	configDirectory = file("${projectDir}/config/checkstyle")
	ignoreFailures = false
}

tasks.withType(Checkstyle) {
	reports {
		xml.required = true
		html.required = true
	}
}
